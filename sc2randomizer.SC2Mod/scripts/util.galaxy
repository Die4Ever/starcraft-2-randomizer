
int rngi(int min, int max) {
    return RandomInt(min, max);
}

fixed rng(fixed min, fixed max) {
    return RandomFixed(min, max);
}

fixed rngexp(fixed origmin, fixed origmax, fixed curve)
{// has trouble with large numbers and small curves, like (100, 250, 0.4) doesn't work but (10, 25, 0.4)*10 works fine
    fixed frange;
    fixed f;
    fixed min;
    fixed max;
    min = origmin;
    max = origmax;
    if(min != 0) {
        min = Pow(min, 1.0/curve);
    }
    max = Pow(max+1.0, 1.0/curve);
    frange = max-min;
    f = rng(0, 1)*frange + min;
    f = Pow(f, curve);
    f = libNtve_gf_ArithmeticRealClamp( f, origmin, origmax );
    return f;
}

fixed randomize_min_max(fixed val, fixed min, fixed max) {
    fixed percent;
    // probably should make all changes far away from 100%, since there will be few things changed
    percent = rng(min, max);
    if( (percent - min) < (max - percent) ) {
        percent = (percent+min) / 2.0;
    } else {
        percent = (percent+max) / 2.0;
    }
    return val * (percent / 100.0);
}

fixed randomize(fixed val, fixed buff) {
    if(min_percent > 100 || max_percent < 100 || buff == 0.0) {
        return randomize_min_max(val, min_percent, max_percent);
    }
    
    if(rng(0,100) < buffs_nerfs) {
        if(buff > 0) {
            return randomize_min_max(val, MaxF(100.0, min_percent), max_percent);
        }
        else {
            return randomize_min_max(val, min_percent, MinF(100.0, max_percent));
        }
    }
    else {
        if(buff > 0) {
            return randomize_min_max(val, min_percent, MinF(100.0, max_percent));
        }
        else {
            return randomize_min_max(val, MaxF(100.0, min_percent), max_percent);
        }
    }
}


void debug(string message)
{
    text t;
    t = StringToText(message);
    TriggerDebugOutput(1, t, false );
    //libNtve_gf_UIErrorMessage( PlayerGroupAll(), t, SoundLink("UI_GenericError", -1));
    //t = StringToText("debug: ") + t;
    //UIDisplayMessage(PlayerGroupAll(), c_messageAreaChat, t);
}

void info(string message)
{
    text t;
    t = StringToText(message);
    TriggerDebugOutput(1, t, false );
    //libNtve_gf_UIErrorMessage( PlayerGroupAll(), t, SoundLink("UI_GenericError", -1));
    //t = StringToText("info: ") + t;
    //UIDisplayMessage(PlayerGroupAll(), c_messageAreaChat, t);
}

void notice(string message)
{
    text t;
    t = StringToText(message);
    TriggerDebugOutput(1, t, false );
    //libNtve_gf_UIErrorMessage( PlayerGroupAll(), t, SoundLink("UI_GenericError", -1));
    UIDisplayMessage(PlayerGroupAll(), c_messageAreaChat, t);
}

void notifyplayer(string message, int player)
{
    text t;
    t = StringToText(message);
    TriggerDebugOutput(1, t, false );
    //libNtve_gf_UIErrorMessage( PlayerGroupAll(), t, SoundLink("UI_GenericError", -1));
    UIDisplayMessage(PlayerGroupSingle(player), c_messageAreaChat, t);
}

void warning(string message)
{
    text t;
    t = StringToText(message);
    TriggerDebugOutput(1, t, false );

    if(bdebug) {
        t = StringToText("warning: ") + t;
        UIDisplayMessage(PlayerGroupAll(), c_messageAreaChat, t);
    }
    //libNtve_gf_UIErrorMessage( PlayerGroupAll(), t, SoundLink("UI_GenericError", -1));
    //t = StringToText("warning: ") + t;
    //UIDisplayMessage(PlayerGroupAll(), c_messageAreaChat, t);
}

void err(string message)
{
    text t;
    t = StringToText(message);
    TriggerDebugOutput(1, t, false );
    //libNtve_gf_UIErrorMessage( PlayerGroupAll(), t, SoundLink("UI_GenericError", -1));
    t = StringToText("error: ") + t;
    UIDisplayMessage(PlayerGroupAll(), c_messageAreaChat, t);
}

void fatal(string message)
{
    text t;
    t = StringToText(message);
    TriggerDebugOutput(1, t, false );
    //libNtve_gf_UIErrorMessage( PlayerGroupAll(), t, SoundLink("UI_GenericError", -1));
    t = StringToText("fatal error: ") + t;
    UIDisplayMessage(PlayerGroupAll(), c_messageAreaChat, t);
}

string SPlayerName(int player)
{
    return TextToString(PlayerName(player));
}


int find_change(int self, string entry, string field)
{
    int i;
    for( i=0; i < race_changes[self].len; i+=1 ) {
        if( race_changes[self].changes[i].entry == entry && race_changes[self].changes[i].field == field ) {
            return i;
        }
    }
    return -1;
}

bool add_change(int self, structref<Change> change)
{
    int len;

    len = race_changes[self].len;
    if( len >= 256 ) {
        err("add_change failed with "+change.entry+" "+change.field+" changing from "+FixedToString(change.oldval,precision)+" to "+FixedToString(change.newval,precision)+", buff "+FixedToString(change.buff, precision));
        return false;
    }
    race_changes[self].changes[len].cat = change.cat;
    race_changes[self].changes[len].unit_entry = change.unit_entry;
    race_changes[self].changes[len].entry = change.entry;
    race_changes[self].changes[len].field = change.field;
    race_changes[self].changes[len].friendly_name = change.friendly_name;
    race_changes[self].changes[len].oldval = change.oldval;
    race_changes[self].changes[len].newval = change.newval;
    race_changes[self].changes[len].buff = change.buff;
    race_changes[self].len += 1;

    debug("add_change with "+change.entry+" "+change.field+" "+change.friendly_name+" changing from "+FixedToString(change.oldval,precision)+" to "+FixedToString(change.newval,precision)+", buff "+FixedToString(change.buff, precision));

    return true;
}


playergroup GetPlayersOfType(int type)
{
    playergroup players;
    int i;

    players = PlayerGroupCopy(PlayerGroupActive());
    for( i=0; i<16; i+=1 ) {
        if( PlayerGroupHasPlayer(players, i) && PlayerType(i) != type ) {
            PlayerGroupRemove(players, i);
        }
    }

    return players;
}

bool PlayerIsActive(int player)
{
    return PlayerGroupHasPlayer(PlayerGroupActive(), player);
}

bool PlayerIsActiveHuman(int player)
{
    return PlayerGroupHasPlayer(GetPlayersOfType(c_playerTypeUser), player);
}

string[100] notes;
bool note_add(string val)
{
    int i;
    for( i=0; i<100; i+=1 ) {
        if( notes[i] == val ) {
            return false;
        }
        if( notes[i] == "" ) {
            info("note_add "+val);
            notes[i] = val;
            return true;
        }
    }
    return false;
}

void notes_log()
{
    int i;
    for( i=0; i<100; i+=1 ) {
        if( notes[i] == "" ) {
            continue;
        }
        TriggerDebugOutput(1, StringToText(notes[i]), false );
    }
}

int get_race_id(int cat, string scope, string entry) {
    string EditorCategories;
    string race;

    if( CatalogFieldExists(scope, "EditorCategories") ) {
        EditorCategories = CatalogFieldValueGet(cat, entry, "EditorCategories", default_player);
        if( ! StringContains(EditorCategories, "ObjectFamily:Melee", c_stringAnywhere, c_stringCase) ) {
            return no_race;
        }
    } else {
        EditorCategories = "";
    }

    if( CatalogFieldExists(scope, "Race") ) {
        race = "Race:" + CatalogFieldValueGet(cat, entry, "Race", default_player);
    }
    else {
        race = EditorCategories;
    }

    if( StringContains(race, "Race:Terr", c_stringAnywhere, c_stringCase) ) {
        return terran;
    }
    else if( StringContains(race, "Race:Prot", c_stringAnywhere, c_stringCase) ) {
        return protoss;
    }
    else if( StringContains(race, "Race:Zerg", c_stringAnywhere, c_stringCase) ) {
        return zerg;
    }
    
    return no_race;
}


string get_name(int cat, string entry) {
    string name;
    string t;

    if( cat == -1 ) {
        return entry;
    }
    if( entry == "OverlordTransport" ) {
        return "Overlord Transport";
    }
    name = CatalogFieldValueGet(cat, entry, "Name", default_player);
    if( name == "" ) {
        return entry;
    } else {
        t = TextToString(StringExternal(name));
        if( t == name ) {
            return entry;
        }
        return t;
    }
}

string change_to_string(structref<Change> change) {
    string s;
    string unit_name;

    if(change.as_string != "") {
        return change.as_string;
    }

    unit_name = change.unit_entry;
    if( change.cat == c_gameCatalogUnit ) {
        unit_name = change.entry;
    }
    unit_name = get_name(c_gameCatalogUnit, unit_name);
    s = unit_name;

    if( change.cat != c_gameCatalogUnit ) {
        s += " " + get_name(change.cat, change.entry);
    }

    if( change.friendly_name != "" ) {
        s += " " + change.friendly_name;
        //s += " {" + change.field + "}";
    } else {
        s += " " + change.field;
    }
    s += " changed from ";

    if( IntToFixed(FixedToInt(change.oldval)) == change.oldval
        && IntToFixed(FixedToInt(change.newval)) == change.newval ) {
        s += FixedToString(change.oldval,0)
            + " to "+FixedToString(change.newval,0);
    }
    else {
        s += FixedToString(change.oldval,precision)
            + " to "+FixedToString(change.newval,precision);
    }

    change.as_string = s;
    return s;
}


// separated by spaces
unitgroup GetUnitsOfTypes(string names, int player)
{
    unitgroup units;
    unitgroup tunits;
    string name;
    int i;

    units = UnitGroupEmpty();
    for(i=1; i<1000; i+=1) {
        name = StringWord(names, i);
        if(name == "") {
            break;
        }
        tunits = UnitGroup(name, player, RegionEntireMap(), UnitFilter(0,0,0,0), 999999 );
        UnitGroupAddUnitGroup(units, tunits);
    }
    return units;
}

int DeleteUnits(string names, int player, int num)
{
    unitgroup units;
    int i;
    unit u;

    units = GetUnitsOfTypes(names, player);
    if(num < 0) {
        num = UnitGroupCount(units, c_unitCountAll) + num;
    } else {
        num = MinI(num, UnitGroupCount(units, c_unitCountAll));
    }
    debug("found "+IntToString(num)+" of "+names);
    if( num < 0 ) {
        return 0;
    }
    for( i=1; i<=num; i+=1 ) {
        u = UnitGroupUnit(units, i);
        UnitRemove(u);
    }
    return num;
}

void copy_change(structref<Change> dst, structref<Change> src) {
    dst.cat = src.cat;
    dst.unit_entry = src.unit_entry;
    dst.entry = src.entry;
    dst.field = src.field;
    dst.friendly_name = src.friendly_name;
    dst.oldval = src.oldval;
    dst.newval = src.newval;
    dst.buff = src.buff;
    dst.as_string = src.as_string;
}


void swap_changes(structref<Change> a, structref<Change> b) {
    Change tc;
    copy_change(tc, a);
    copy_change(a, b);
    copy_change(b, tc);
}


typedef string[128] string_array;
int sort_faction_partition(arrayref<changes_array> changes, int start, int end) {
    string pivot;
    int pivotIndex;
    int i;
    int j;
    int count;

    pivot = change_to_string(changes[start]);
    count = 0;
    for(i=start+1; i<=end; i+=1) {
        if(StringCompare(change_to_string(changes[i]), pivot, true) <= 0) {
            count += 1;
        }
    }

    pivotIndex = start + count;
    swap_changes(changes[start], changes[pivotIndex]);

    i = start;
    j = end;

    while (i < pivotIndex && j > pivotIndex) {
        while(StringCompare(change_to_string(changes[i]), pivot, true) <= 0) {
            i+=1;
        }

        while(StringCompare(change_to_string(changes[j]), pivot, true) > 0) {
            j-=1;
        }

        if(i < pivotIndex && j > pivotIndex) {
            swap_changes(changes[i], changes[j]);
            i+=1;
            j-=1;
        }
    }

    return pivotIndex;
}


void sort_faction_changes(arrayref<changes_array> changes, int start, int end) {
    int i;
    string a;
    string b;
    int cmp;
    Change t;
    int p;

    if(start >= end) {
        return;
    }

    p = sort_faction_partition(changes, start, end);
    sort_faction_changes(changes, start, p-1);
    sort_faction_changes(changes, p+1, end);
}


void sort_changes() {
    int i;

    for(i=0; i< max_factions*4; i+=1) {
        if(race_changes[i].len > 0) {
            Wait(0.01, c_timeReal);
            sort_faction_changes(race_changes[i].changes, 0, race_changes[i].len-1);
        }
    }
}
