include "TriggerLibs/NativeLib"

// https://mapster.talv.space/galaxy/reference#catalog
// https://sc2mapster.fandom.com/wiki/Tutorials

//string number_types;
fixed min_percent;
fixed max_percent;
int seed;
int changes_per_race;
int changes_per_unit;

struct Change {
    string entry;
    string field;
    fixed oldval;
    fixed newval;
};

struct RaceChanges {
    string name;
    Change[64] changes;
    int len;
};
const int no_race=0;
const int terran=1;
const int protoss=2;
const int zerg=3;
RaceChanges[4] race_changes;

struct RandomizedField {
    //int cat;
    int type;
    string scope;
    string name;
};
RandomizedField[100] randomized_fields;
const int type_fixed = 0;
const int type_int = 1;
// scopes, is this going too far?
const int type_unit = 2;
const int type_ability = 3;
const int type_effect = 4;
const int type_weapon = 5;
const int type_upgrade = 6;
const int type_behavior = 7;
// field types, is this going too far?
const int type_SCost = 8;
const int type_SAbilMergeInfo = 9;
const int type_SAbilMorphInfo = 10;
const int type_SAbilResearchInfo = 11;
const int type_SAbilBuildInfo = 12;
const int type_SAbilTrainInfo = 13;
const int type_SAbilArmMagazineInfo = 14;
const int type_SAbilWarpTrainInfo = 15;

void AddRandomizedField(string scope, int type, string name)
{
    int i;
    for( i=0; i<100; i+=1 ) {
        if( randomized_fields[i].name == "" ) {
            randomized_fields[i].scope = scope;
            randomized_fields[i].type = type;
            randomized_fields[i].name = name;
            return;
        }
    }
}

int GetRandomField(string scope)
{
    int num;
    int i;

    num = 0;
    for( i=0; i<100; i+=1 ) {
        if( randomized_fields[i].scope == scope && randomized_fields[i].name != "" ) {
            num+=1;
        }
    }

    num = RandomInt(0, num-1);
    for( i=0; i<100; i+=1 ) {
        if( randomized_fields[i].scope == scope && randomized_fields[i].name != "" ) {
            if( num == 0 ) {
                return i;
            }
            num-=1;
        }
    }

    return -1;
}

string[100] notes;
bool note_add(string val)
{
    int i;
    for( i=0; i<100; i+=1 ) {
        if( notes[i] == val ) {
            return false;
        }
        if( notes[i] == "" ) {
            notes[i] = val;
            return true;
        }
    }
    return false;
}

void notes_log()
{
    int i;
    for( i=0; i<100; i+=1 ) {
        if( notes[i] == "" ) {
            continue;
        }
        TriggerDebugOutput(1, StringToText(notes[i]), false );
    }
}

void debug(string message)
{
    TriggerDebugOutput(1, StringToText(message), false );
}

void info(string message)
{
    TriggerDebugOutput(1, StringToText(message), false );
}

void warning(string message)
{
    TriggerDebugOutput(1, StringToText(message), false );
}

void error(string message)
{
    TriggerDebugOutput(1, StringToText(message), false );
}

void fatal(string message)
{
    TriggerDebugOutput(1, StringToText(message), false );
}

bool add_change(int self, string entry, string field, fixed oldval, fixed newval)
{
    int len;

    len = race_changes[self].len;
    if( len >= 64 ) {
        error("add_change failed with "+entry+" "+field+" changing from "+FixedToString(oldval,5)+" to "+FixedToString(newval,5));
        return false;
    }
    race_changes[self].changes[len].entry = entry;
    race_changes[self].changes[len].field = field;
    race_changes[self].changes[len].oldval = oldval;
    race_changes[self].changes[len].newval = newval;
    race_changes[self].len += 1;

    //message = "add_change with "+entry+" "+field+" changing from "+FixedToString(oldval,5)+" to "+FixedToString(newval,5);
    //TriggerDebugOutput(1, StringToText(message), false );

    return true;
}

void log_changes(int self)
{
    int i;
    string message;

    message = race_changes[self].name + " Changes: ";

    for( i=0; i < race_changes[self].len; i+=1 ) {
        if( i > 0 ) {
            message += ", ";
        }
        message +=
            race_changes[self].changes[i].entry+" "+race_changes[self].changes[i].field
            + " changed from "
            + FixedToString(race_changes[self].changes[i].oldval,5)
            + " to "+FixedToString(race_changes[self].changes[i].newval,5);
    }

    debug(message );

    //UIDisplayMessage( PlayerGroupAll(), c_messageAreaChat, StringToText(message) );
    //UIShowTextCrawl( PlayerGroupAll(), StringToText(race_changes[self].name + " Changes: "), StringToText(message), 6, SoundLink("Silent", -1), SoundLink("Silent", -1));
    // https://mapster.talv.space/galaxy/reference/dialog-create
    // https://mapster.talv.space/galaxy/reference#dialog-dialog-items
}

fixed rng(fixed min, fixed max) {
    return RandomFixed(min, max);
}

fixed randomize(fixed val) {
    // probably should make all changes far away from 100%, since there will be few things changed
    return val * rng(min_percent, max_percent) / 100.0;
}


bool set_field_string(int cat, string entry, string field, string value)
{
    playergroup allplayers;
    int player;
    int num_players;
    int i;

    allplayers = PlayerGroupAll();
    num_players = PlayerGroupCount(allplayers);
    for( i=1; i<=num_players; i+=1 ) {
        player = PlayerGroupPlayer( allplayers, i );
        if( ! CatalogFieldValueSet(cat, entry, field, player, value) ) {
            return false;
        }
    }
    return true;
}


bool set_field_fixed(int cat, string entry, string field, fixed value)
{
    playergroup allplayers;
    int player;
    int num_players;
    int i;
    allplayers = PlayerGroupAll();
    num_players = PlayerGroupCount(allplayers);
    for( i=1; i<=num_players; i+=1 ) {
        player = PlayerGroupPlayer( allplayers, i );
        if( ! libNtve_gf_CatalogFieldValueSetAsReal(cat, entry, field, player, value) ) {
            return false;
        }
    }
    return true;
}


bool rando_field_fixed(int cat, string entry, string field, int changes_id)
{
    fixed getval;
    fixed setval;
    fixed value;

    getval = libNtve_gf_CatalogFieldValueGetAsReal(cat, entry, field, 1);
    if( getval == 0 ) {
        error(entry+" "+field+" failed to read");
        return false;
    }
    value = randomize(getval);
    if( value == getval ) {
        return false;
    }
    if( ! set_field_fixed(cat, entry, field, value ) ) {
        error(entry+" "+field+" was "+FixedToString(getval,5)+", failed to change to "+FixedToString(value,5));
        return false;
    }
    setval = libNtve_gf_CatalogFieldValueGetAsReal(cat, entry, field, 1);
    if( getval == setval ) { return false; }

    add_change(changes_id, entry, field, getval, setval);

    if( field == "LifeMax" ) {
        if( ! set_field_fixed(cat, entry, "LifeStart", value) ) {
            error(entry+" LifeStart failed to set to "+FixedToString(value,5));
            return false;
        }
    }
    if( field == "ShieldsMax" ) {
        if( ! set_field_fixed(cat, entry, "ShieldsStart", value) ) {
            error(entry+" ShieldsStart failed to set to "+FixedToString(value,5));
            return false;
        }
    }
    return true;
}


bool rando_field_int(int cat, string entry, string field, int changes_id)
{
    int getval;
    int setval;
    int value;

    getval = CatalogFieldValueGetAsInt(cat, entry, field, 1);
    if( getval == 0 ) {
        error(entry+" "+field+" failed to read");
        return false;
    }
    value = FixedToInt( randomize(getval) );
    if( value == getval ) {
        return false;
    }
    if( ! set_field_string(cat, entry, field, IntToString(value) ) ) {
        error(entry+" "+field+" was "+IntToString(getval)+", failed to change to "+IntToString(value));
        return false;
    }
    setval = CatalogFieldValueGetAsInt(cat, entry, field, 1);
    if( getval == setval ) { return false; }

    add_change(changes_id, entry, field, getval, setval);

    if( field == "LifeMax" ) {
        if( ! set_field_fixed(cat, entry, "LifeStart", value) ) {
            error(entry+" LifeStart failed to set to "+FixedToString(value,5));
            return false;
        }
    }
    if( field == "ShieldsMax" ) {
        if( ! set_field_fixed(cat, entry, "ShieldsStart", value) ) {
            error(entry+" ShieldsStart failed to set to "+FixedToString(value,5));
            return false;
        }
    }
    return true;
}


int get_race_id(int cat, string scope, string entry) {
    string EditorCategories;
    string race;

    if( CatalogFieldExists(scope, "EditorCategories") ) {
        EditorCategories = CatalogFieldValueGet(cat, entry, "EditorCategories", 1);
        if( ! StringContains(EditorCategories, "ObjectFamily:Melee", c_stringAnywhere, c_stringCase) ) {
            return no_race;
        }
    } else {
        EditorCategories = "";
    }

    if( CatalogFieldExists(scope, "Race") ) {
        race = "Race:" + CatalogFieldValueGet(cat, entry, "Race", 1);
    }
    else {
        race = EditorCategories;
    }

    if( StringContains(race, "Race:Terr", c_stringAnywhere, c_stringCase) ) {
        return terran;
    }
    else if( StringContains(race, "Race:Prot", c_stringAnywhere, c_stringCase) ) {
        return protoss;
    }
    else if( StringContains(race, "Race:Zerg", c_stringAnywhere, c_stringCase) ) {
        return zerg;
    }
    
    return no_race;
}


void randomize_start() {
    // starting workers, mineral patches, geysers...
}


void randomize_workers() {
}


int randomize_fields(int cat, string scope, string entry, int race_id, int num_fields) {
    int i;
    int slot;
    int num;
    bool[100] mask;
    bool is_array;
    int len;

    num = 0;
    for( i=0; num<num_fields && i<32; i+=1 ) {
        slot = GetRandomField(scope);
        if( slot == -1 ) {
            return num;
        }
        if( mask[slot] ) { continue; }
        mask[slot] = true;

        // will need this if I want to use randomize_fields for calling randomize_ability and stuff like that
        is_array = CatalogFieldIsArray(scope, field);
        if( is_array ) {
            len = CatalogFieldValueCount(cat, entry, field, 1);
        }

        // probably move this into a new function randomize_field so I can loop over it if it's an array
        if( randomized_fields[slot].type == type_fixed ) {
            if( rando_field_fixed(cat, entry, randomized_fields[slot].name, race_id) ) {
                num+=1;
            }
        }
        else if( randomized_fields[slot].type == type_int ) {
            if( rando_field_int(cat, entry, randomized_fields[slot].name, race_id) ) {
                num+=1;
            }
        }
    }

    return num;
}


void randomize_effect(string entry, int race_id, int num_fields) {
    int cat = c_gameCatalogEffect;
    string scope;
    if( entry == "" ) {
        return;
    }

    scope = CatalogEntryScope(cat, entry);
    randomize_fields(cat, scope, entry, race_id, num_fields);

    // PsiStormPersistent is a CEffectCreatePersistent with a PeriodCount, PeriodEffectArray, PeriodicPeriodArray
    // PsiStormSearch is a CEffectEnumArea with a AreaArray[i].Radius, AreaArray[i].Effect
    // PsiStormApplyBehavior is a CEffectApplyBehavior with a Behavior == PsiStorm (a CBehaviorBuff)
    // PsiStormDamageInitial is a CEffectDamage with an Amount == 10, PsiStormDamage is the same

    // ArbiterMPRecallSet is a CEffectSet with an array EffectArray
    // ArbiterMPRecallTeleport is a CEffectTeleport with a PlacementRange of 15, Range of 1000

    note_add(CatalogEntryScope(cat, entry));
}


void randomize_weapon(string entry, int race_id, int num_fields) {
    int cat = c_gameCatalogWeapon;
    string scope;

    if( entry == "" ) {
        return;
    }

    scope = CatalogEntryScope(cat, entry);
    randomize_fields(cat, scope, entry, race_id, num_fields);

    // CWeapon_DisplayEffect is where the damage is defined?

    // scopes
    // CWeaponLegacy has Period, Range, MinimumRange, MinScanRange, SCost Cost, 
    // InterceptorBeam is the only CWeaponStrafe with a Period, Range, Arc

    //set_add(CatalogEntryScope(cat, entry));
}


void randomize_upgrade(string entry, int race_id, int num_fields) {
    int cat = c_gameCatalogUpgrade;
    string scope;

    if( entry == "" ) {
        return;
    }

    // CUpgrade BattlecruiserBehemothReactor has <EffectArray Reference="Unit,Battlecruiser,EnergyStart" Value="25"/>
    // maybe if EffectArray[i].Operation is anything except Set, then it can be randomized
    scope = CatalogEntryScope(cat, entry);
    randomize_fields(cat, scope, entry, race_id, num_fields);
}


void randomize_ability(string entry, int race_id, int num_fields) {
    int cat = c_gameCatalogAbil;
    string scope;
    string type;
    int len;
    int i;
    string val;

    if( entry == "" ) {
        return;
    }

    scope = CatalogEntryScope(cat, entry);
    randomize_fields(cat, scope, entry, race_id, num_fields);

    // scopes
    // CAbilBuild
    // CAbilMorphPlacement
    // CAbilMove
    // CAbilStop
    // CAbilBuildable
    // CAbilQueue
    // CAbilArmMagazine
    // CAbilResearch
    // CAbilMorph
    // CAbilAttack
    // CAbilWarpable
    // CAbilEffectTarget
    // CAbilTrain
    // CAbilRally
    // CAbilEffectInstant
    // CAbilAugment
    // CAbilMerge
    // CAbilTransport
    // CAbilMergeable
    // CAbilBehavior
    // CAbilRedirectInstant
    // CAbilRedirectTarget

    // BarracksTechLabMorph is a CAbilMorph with InfoArray SAbilMorphInfo, and with Cost SCost
    // FactoryLand is a CAbilMorphPlacement with InfoArray SAbilMorphInfo, and with Cost SCost
    // FactoryTechLabResearch is a CAbilResearch with InfoArray SAbilResearchInfo
        // with InfoArray[3].Upgrade == TerranInfantryWeaponsLevel1, .Time=160, .Resource[Minerals/Vespene]
    // Rally is a CAbilRally with InfoArray SAbilRallyInfo
    // BuildNydusCanal is a CAbilBuild with InfoArray SAbilBuildInfo has [i].Unit for unit name and [i].Time for build time
    // TrainQueen is a CAbilTrain with InfoArray SAbilTrainInfo
    // ArmSiloWithNuke is a CAbilArmMagazine with InfoArray SAbilArmMagazineInfo, and with Cost SCost
    // WarpGateTrain is a CAbilWarpTrain with InfoArray SAbilWarpTrainInfo

    // GhostCloak is a CAbilBehavior with Cost SCost
        // has an array of structs named Cost, Cost[i].Charge, .Cooldown.TimeStart, .Cooldown.TimeUse, .Vital.Life, .Vital.Shields, .Vital.Energy, .VitalFraction
    
    // ArchonWarp is a CAbilMerge with InfoArray 
        // Info is a SAbilMergeInfo with Info.Resource.Minerals/Vespene, Info.Time, Info.Unit==Archon, Info.Cooldown, Info.Vital, Info.VitalFraction (seems to be same type as a CAbilBehavior.Cost)
    
    // Charge is a CAbilAugment with Cost SCost
        // has struct Cooldown (probably same type as Cooldown in CAbilBehavior)

    // TacNukeStrike is a CAbilEffectTarget with Cost SCost
        // has arrays for Effect, FinishTime (fixed), Range (fixed or maybe int?)
    
    // NydusCanalTransport is a CAbilTransport with InfoArray 
        // has MaxCargoCount, MaxCargoSize, Range, TotalCargoSpace, UnloadPeriod

    // BuildInProgress is a CAbilBuildable with InfoArray 
    // que5Addon is a CAbilQueue with InfoArray 
    // stop is a CAbilStop with InfoArray 
    // attack is a CAbilAttack with InfoArray 
    // move is a CAbilMove with InfoArray 
    // GhostHoldFire is a CAbilEffectInstant with InfoArray 
    // StimpackRedirect is a CAbilRedirectInstant with InfoArray 
    // AttackRedirect is a CAbilRedirectTarget with InfoArray 
    // Warpable is a CAbilWarpable with InfoArray 
    // Mergeable is a CAbilMergeable with InfoArray 

    //type = CatalogFieldType(scope, "Cost");
    //if( note_add(scope) ) {
    //    note_add(entry+" is a "+scope+" with Cost "+type);
    //}
}


void randomize_unit(string entry, int race_id, int num_fields) {
    int cat = c_gameCatalogUnit;
    string scope = "CUnit";
    int i;
    int len;
    int slot;
    string val;

    if( entry == "" ) { return; }

    randomize_fields(cat, scope, entry, race_id, num_fields);

    // check abilities, weapons, and upgrades
    len = CatalogFieldValueCount(cat, entry, "AbilArray", 1);
    for( i=0; i<len; i+=1 ) {
        val = CatalogFieldValueGet(cat, entry, "AbilArray["+IntToString(i)+"].Link", 1);
        randomize_ability(val, race_id, num_fields);
    }

    len = CatalogFieldValueCount(cat, entry, "WeaponArray", 1);
    for( i=0; i<len; i+=1 ) {
        val = CatalogFieldValueGet(cat, entry, "WeaponArray["+IntToString(i)+"].Link", 1);
        randomize_weapon(val, race_id, num_fields);
    }
}


void randomize_units(int changes_per_race) {
    int cat = c_gameCatalogUnit;
    string scope = "CUnit";
    string entry;
    string cost_category;
    int num_data;
    int race_id;
    int i;
    int slot;

    num_data = CatalogEntryCount(cat);

    for( i=1; i<=num_data; i+=1 ) {

        if( race_changes[terran].len >= changes_per_race
            && race_changes[protoss].len >= changes_per_race
            && race_changes[zerg].len >= changes_per_race
        ) {
            break;
        }
        GameSetSeed( 5 + seed + i);
        slot = RandomInt(1, num_data);

        entry = CatalogEntryGet(cat, slot);
        race_id = get_race_id(cat, scope, entry);
        if( race_id == no_race ) {
            continue;
        }
        if( race_changes[race_id].len >= changes_per_race ) {
            continue;
        }

        cost_category = CatalogFieldValueGet(cat, entry, "CostCategory", 1);
        if( cost_category != "Army" && cost_category != "Technology" ) {
            continue;
        }

        GameSetSeed( 5 + seed + slot);
        randomize_unit(entry, race_id, changes_per_unit);
    }

    GameSetSeed(seed);
}


void main () {
    bool seed_was_locked;
    int oldseed;
    //int changes_per_category;
    int i;

    seed=13371;
    min_percent = 50;
    max_percent = 150;
    changes_per_race = 50;
    changes_per_unit = 1;
    
    AddRandomizedField("CUnit", type_int, "CostResource[Minerals]");
    AddRandomizedField("CUnit", type_int, "CostResource[Vespene]");
    AddRandomizedField("CUnit", type_int, "Food");
    AddRandomizedField("CUnit", type_int, "LifeMax");
    AddRandomizedField("CUnit", type_int, "ShieldsMax");
    AddRandomizedField("CUnit", type_int, "EnergyMax");
    AddRandomizedField("CUnit", type_fixed, "Acceleration");
    AddRandomizedField("CUnit", type_fixed, "Speed");

    AddRandomizedField("CWeaponLegacy", type_fixed, "Range");
    AddRandomizedField("CWeaponLegacy", type_fixed, "Period");
    AddRandomizedField("CWeaponStrafe", type_fixed, "Range");
    AddRandomizedField("CWeaponStrafe", type_fixed, "Period");

    AddRandomizedField("CEffectDamage", type_int, "Amount");

    seed_was_locked = GameIsSeedLocked();
    GameSetSeed(seed);

    //number_types = " CFixed CGameTime CGameAcceleration CGameSpeed CGameRate SAccumulatedFixed int32 ";

    randomize_workers();
    randomize_start();
    randomize_units(changes_per_race);

    race_changes[no_race].name = "General";
    race_changes[terran].name = "Terran";
    race_changes[protoss].name = "Protoss";
    race_changes[zerg].name = "Zerg";
    for(i=0; i<4 ; i+=1 ) {
        log_changes(i);
    }

    notes_log();
    GameSetSeedLocked(seed_was_locked);
}
